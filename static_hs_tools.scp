[ITEMDEF i_static_hs_catalogue]
DEFNAME=i_static_hs_catalogue
ID=02d4d
NAME=Static Building Catalogue
TYPE=t_normal
FLIP=1
VALUE=20
WEIGHT=1.0
DUPELIST=02d4e
ON=@Create
	attr=010
ON=@DClick
	if (<distance> < 3)
		src.ctag.menu_page=1
		src.ctag.menu_val=1 10
		src.f_static_allsigns_namelist
		src.f_resenddialog d_static_hs_catalogue
	endif
	return 1

[DIALOG d_static_hs_catalogue]
50,50
local.count=<eval <serv.list.temp_<src>.count>-1>
if !<local.count>
	return 1
endif
page 0
call fd_static_hs_catalogue_bg
dorigin 150 138
for <src.ctag0.menu_val>
	if <dlocal._for> <= <dlocal.count>
		args=<serv.list.temp_<src>.<dlocal._for>>
		ref1=<argv[1]>
		local.maxstorage=<ref1.room.dtag0.maxstorage>
		dhtmlgump - *20 200 20 0 0 <def.bfont_hs_text1><ref1.room.name>
		if (<ref1.tag0.forsale_new>)
			button -34 - 4005 4007 1 0 <eval <dlocal._for>+100>
			dhtmlgump +266 +3 68 20 0 0 <def.center><def.bfont_hs_text1><def.bfont_size1><f_numformat <ref1.room.dtag0.value>>
		elif (<ref1.tag0.forsale>)
			ref2=<serv.list.static_hs_<ref1>.0> // owner
			button -34 - 4005 4007 1 0 <eval <dlocal._for>+100>
			dhtmlgump +266 +3 68 20 0 0 <def.center><def.bfont_hs_text2><def.bfont_size1><f_numformat <ref1.dtag0.price>>
			tooltip <ddef0.empty_cliloc>,@Sold by <ref2.f_hs_noto_bfontcolor><ref2.name><def.bfont_white>
		else
			gumppic -34 - 4020 0
			dhtmlgump +266 +3 68 20 0 0 <def.center><def.bfont_hs_text1><def.bfont_size1>Private
			tooltip <ddef0.empty_cliloc>,@Sold
		endif
		dhtmlgump +210 +3 46 20 0 0 <def.center><def.bfont_hs_text3><def.bfont_size1><f_numformat <dlocal.maxstorage>>
		dhtmlgump +356 +3 140 20 0 0 <def.bfont_hs_text3><def.bfont_size1><ref1.region.name>
	endif
endfor

[DIALOG d_static_hs_catalogue button]
on=0
on=101 5000
	local.opt=<eval <argn1>-100>
	args=<serv.list.temp_<src>.<dlocal.opt>>
	ref1=<argv[1]>
	if !(<ref1.tag0.forsale_new> || <ref1.tag0.forsale>)
		src.sysmessage @,,1 <ref1.name> is no longer on the market.
	else
		src.sysmessage @1151,,1 Tracking <ref1.room.name> in <ref1.region.name> [<ref1.p>].
		src.arrowquest 0,0,0
		src.arrowquest <ref1.p.x>,<ref1.p.y>,0
		if <src.isgm>
			go=<ref1.p>
		endif
	endif
	sdialog d_static_hs_catalogue

on=5001 5002
	local.count=<eval <serv.list.temp_<src>.count>-1>
	local.page_max=<eval ((<dlocal.count>-1)/10)+1>
	if <dargn1>==5001 // back
		src.ctag0.menu_page --
		if <src.dctag0.menu_page> < 1
			src.ctag.menu_page=<dlocal.page_max>
		endif
	else // next
		src.ctag0.menu_page ++
		if <src.dctag0.menu_page> > <dlocal.page_max>
			src.ctag.menu_page=1
		endif
	endif
	local.x=<eval ((<src.dctag0.menu_page>-1)*10)+1>
	local.y=<eval <src.dctag0.menu_page>*10>
	if (<dlocal.y> > <dlocal.count>)
		local.y=<dlocal.count>
	endif
	src.ctag.menu_val=<dlocal.x> <dlocal.y>
	sdialog d_static_hs_catalogue

on=5101 5104 // sort desc
	src.ctag.menu_page=1
	src.ctag.menu_val=1 10
	if <dargn1>==5101
		src.f_static_allsigns_namelist
	elif <dargn1>==5102
		src.f_static_allsigns_storagelist
	elif <dargn1>==5103
		src.f_static_allsigns_pricelist
	else // 5104
		src.f_static_allsigns_regionlist
	endif
	sdialog d_static_hs_catalogue

on=5105 5108 // sort asc
	src.ctag.menu_page=1
	src.ctag.menu_val=1 10
	if <dargn1>==5105
		src.f_static_allsigns_namelist
	elif <dargn1>==5106
		src.f_static_allsigns_storagelist
	elif <dargn1>==5107
		src.f_static_allsigns_pricelist
	else // 5108
		src.f_static_allsigns_regionlist
	endif
	f_reverse_list temp_<src>
	sdialog d_static_hs_catalogue



[EVENTS e_temp_list]
ON=@Logout
	serv.list.temp_<uid>.clear
	events=-e_temp_list

ON=@UserQuestArrowClick
	if (<dargn1>==1)
		arrowquest 0,0,0
		return 1
	endif

ON=@Destroy
	serv.list.temp_<uid>.clear


[FUNCTION fd_static_hs_catalogue_bg]
local.page_max=<eval ((<dlocal.count>-1)/10)+1>
dorigin 100 100
resizepic - - 5054 551 292
gumppictiled *4 *6 540 20 2624
checkertrans - - 540 20
button +522 +2 22150 22152 1 0 0
dhtmlgump - +1 540 20 0 0 <def.bfont_hs_yellow><def.center>STATIC BUILDINGS [<dlocal.count>]
gumppictiled - *24 540 20 2624
checkertrans - - 540 20
dhtmlgump *54 *1 120 20 0 0 <def.bfont_hs_title>Property Name
button -12 +4 2437 2438 1 0 5105
button +92 +4 2435 2436 1 0 5102
dhtmlgump *200 - 120 20 0 0 <def.bfont_hs_title>Storage
button -12 +4 2437 2438 1 0 5106
button +48 +4 2435 2436 1 0 5102
dhtmlgump *76 - 120 20 0 0 <def.bfont_hs_title>Cost
button -12 +4 2437 2438 1 0 5107
button +30 +4 2435 2436 1 0 5103
dhtmlgump *76 - 120 20 0 0 <def.bfont_hs_title>Region
button -12 +4 2437 2438 1 0 5108
button +39 +4 2435 2436 1 0 5104

dorigin 104 154
gumppictiled - - 540 210 2624
checkertrans - - 540 210
gumppictiled - *214 540 20 2624
checkertrans - - 540 20
dhtmlgump *20 *1 240 20 0 0 <def.bfont_hs_text3>Total Gold <def.bfont_hs_text2><f_numformat <src.gold>> <def.bfont_hs_text3>gp
dhtmlgump +456 - 240 20 0 0 <def.bfont_hs_text3>Page <src.dctag0.menu_page>/<dlocal.page_max>
if <dlocal.count> > 10
	button 246 108 5603 5607 1 0 5001	// back // 9910 9909
	button 479 108 5601 5605 1 0 5002	// next // 9904 9903
endif




[FUNCTION f_static_allsigns_namelist]
ref1=<uid> // player
serv.list.temp_<ref1>.clear
serv.list.temp_<ref1>.add=1,<ref1> // not really used
if !(<ref1.isevent.e_temp_list>)
	ref1.events=+e_temp_list
endif
forinstances i_sign_static_house
	ref2=<uid>
	serv.list.temp_<ref1>.add="<ref2.room.name>,<ref2>"
endfor
forinstances i_sign_static_house2
	ref2=<uid>
	serv.list.temp_<ref1>.add="<ref2.room.name>,<ref2>"
endfor
serv.list.temp_<ref1>.sort asc

[FUNCTION f_static_allsigns_storagelist]
ref1=<uid> // player
serv.list.temp_<ref1>.clear
serv.list.temp_<ref1>.add=1,<ref1> // not really used
if !(<ref1.isevent.e_temp_list>)
	ref1.events=+e_temp_list
endif
forinstances i_sign_static_house
	ref2=<uid>
	serv.list.temp_<ref1>.add="<ref2.room.dtag0.maxstorage>,<ref2>"
endfor
forinstances i_sign_static_house2
	ref2=<uid>
	serv.list.temp_<ref1>.add="<ref2.room.dtag0.maxstorage>,<ref2>"
endfor
f_sort_list_0_desc temp_<ref1>


[FUNCTION f_static_allsigns_pricelist]
ref1=<uid> // player
serv.list.temp_<ref1>.clear
serv.list.temp_<ref1>.add=1,<ref1> // not really used
if !(<ref1.isevent.e_temp_list>)
	ref1.events=+e_temp_list
endif
forinstances i_sign_static_house
	ref2=<uid>
	if (<ref2.tag0.forsale>)
		local.price=<ref2.dtag0.price>
	else
		local.price=<ref2.room.dtag0.value>
	endif
	serv.list.temp_<ref1>.add="<dlocal.price>,<ref2>"
endfor
forinstances i_sign_static_house2
	ref2=<uid>
	if (<ref2.tag0.forsale>)
		local.price=<ref2.dtag0.price>
	else
		local.price=<ref2.room.dtag0.value>
	endif
	serv.list.temp_<ref1>.add="<dlocal.price>,<ref2>"
endfor
f_sort_list_0_desc temp_<ref1>


[FUNCTION f_static_allsigns_regionlist]
ref1=<uid> // player
serv.list.temp_<ref1>.clear
serv.list.temp_<ref1>.add=1,<ref1> // not really used
if !(<ref1.isevent.e_temp_list>)
	ref1.events=+e_temp_list
endif
forinstances i_sign_static_house
	ref2=<uid>
	serv.list.temp_<ref1>.add="<ref2.region.name>,<ref2>"
endfor
forinstances i_sign_static_house2
	ref2=<uid>
	serv.list.temp_<ref1>.add="<ref2.region.name>,<ref2>"
endfor
serv.list.temp_<ref1>.sort asc



[FUNCTION f_sort_list_0_desc] // uses <argv[0]> for sorting
local.list=<args>
for 0 <eval <serv.list.<local.list>.count>-1>
	for 0 <eval (<serv.list.<local.list>.count>-1)-<dlocal._for>>
		local.n1=<strarg <serv.list.<local.list>.<dlocal._for>>>
		local.n2=<strarg <serv.list.<local.list>.<eval <dlocal._for>+1>>>
		if <dlocal.n1> < <dlocal.n2>
			local.temp=<serv.list.<local.list>.<dlocal._for>>
			serv.list.<local.list>.<dlocal._for>=<serv.list.<local.list>.<eval <dlocal._for>+1>>
			serv.list.<local.list>.<eval <dlocal._for>+1>=<local.temp>
		endif
	endfor
endfor

/// Reverses any list (turns descending â†’ ascending)
[FUNCTION f_reverse_list]
local.src=<args> // name of source list
local.dest=<local.src>_rev // destination list (or choose your own name)
for <eval <serv.list.<local.src>.count>-1> 0
	serv.list.<local.dest>.add <serv.list.<local.src>.<dlocal._for>>
endfor
serv.list.<local.src>.clear
for 0 <eval <serv.list.<local.dest>.count>-1>
	serv.list.<local.src>.add <serv.list.<local.dest>.<dlocal._for>>
endfor
serv.list.<local.dest>.clear



[EOF]